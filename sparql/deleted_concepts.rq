PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX owl:     <http://www.w3.org/2002/07/owl#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sh:      <http://raw.github.com/jneubert/skos-history/master/skos-history.ttl/>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>

# identify concepts deleted with a certain version

# (In published vocabularies, the deletion of concepts should be regarded
# as bad practice, because they still may be referenced elsewhere.
# Cosider using owl:deprecated instead)

SELECT  ?concept ?prefLabel
WHERE
  { 
    # fixed version, will be parameter or current version
    BIND(<http://zbw.eu/stw/version/8.10> AS ?version)

    GRAPH ?version
      { 
        # this assumes that only one delta to a prior version exists
        # (not multiple, overlapping deltas!)
        ?delta sh:deltaTo ?version .

        # identify deletions parts/graphs
        ?delta dcterms:hasPart ?deletions .
        ?deletions rdf:type sh:SchemeDeltaDeletions

        # for each deleted concept, a deleted prefLabel must exist ...
        GRAPH ?deletions
          { ?concept skos:prefLabel ?prefLabel }


        # ... without anything remaining for this concept in the current version
        FILTER NOT EXISTS { graph ?version
            { ?concept ?p [] }
        }
      }
  }
ORDER BY ?concept

