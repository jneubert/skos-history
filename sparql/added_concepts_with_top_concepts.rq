PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dsv: <http://purl.org/iso25964/DataSet/Versioning#>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>
PREFIX sh: <http://purl.org/skos-history/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX skosxl: <http://www.w3.org/2008/05/skos-xl#>
PREFIX xhv: <http://www.w3.org/1999/xhtml/vocab#>
#
# Identify concepts inserted with a certain version, and show their top concepts
#
# For STW, top concepts always are defined
# as a subsection of the thesaurus; a concept may have
# multiple top concepts.
#
# For TheSoz, as of v0.93, no top concepts are defined.
#
SELECT distinct ?topConcept (str(?topLabel) AS ?topConceptLabel) (?concept AS ?addedConcept) (str(?prefLabel) AS ?addedConceptLabel)
WHERE {
  # parameters
  VALUES ( ?versionHistoryGraph ?language ) {
    ( <http://zbw.eu/stw/version> "en" )
  }
  GRAPH ?versionHistoryGraph {
    # the compared versions default to the current and the previous one
    ?versionset dsv:currentVersionRecord/xhv:prev/dc:identifier ?oldVersion .
    ?versionset dsv:currentVersionRecord/dc:identifier ?newVersion .
    # get the delta and via that the relevant graphs
    ?delta a sh:SchemeDelta ;
      sh:deltaFrom/dc:identifier ?oldVersion ;
      sh:deltaTo/dc:identifier ?newVersion ;
      sh:deltaFrom/sh:usingNamedGraph/sd:name ?oldVersionGraph ;
      sh:deltaTo/sh:usingNamedGraph/sd:name ?newVersionGraph ;
      dcterms:hasPart ?insertions ;
      dcterms:hasPart ?deletions .
    ?insertions a sh:SchemeDeltaInsertions ;
      sh:usingNamedGraph/sd:name ?insertionsGraph .
    ?deletions a sh:SchemeDeltaDeletions ;
      sh:usingNamedGraph/sd:name ?deletionsGraph .
  }
  # for each inserted concept, a (newly inserted) prefLabel must exist ...
  GRAPH ?insertionsGraph {
    ?concept skos:prefLabel []
  }
  # ... which did not exist in the old version
  FILTER NOT EXISTS {
    GRAPH ?oldVersionGraph {
      ?concept ?p []
    }
  }
  GRAPH ?newVersionGraph {
    ?concept skosxl:prefLabel/skosxl:literalForm | skos:prefLabel ?prefLabel .
    # get the top concept(s)
    ?concept skos:broader+ ?topConcept .
    ?scheme skos:hasTopConcept ?topConcept .
    ?topConcept skosxl:prefLabel/skosxl:literalForm | skos:prefLabel ?topLabel .
  }
  # restrict output to a certain language
  FILTER (lang(?prefLabel) = ?language && lang(?topLabel) = ?language)
}
ORDER BY ?topLabel ?prefLabel
