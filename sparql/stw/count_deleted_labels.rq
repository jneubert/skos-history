PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dsv: <http://purl.org/iso25964/DataSet/Versioning#>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>
PREFIX sh: <http://purl.org/skos-history/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xhv: <http://www.w3.org/1999/xhtml/vocab#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
# Count deleted labels in a certain language per version
#
SELECT ?version (str(count(distinct ?label)) as ?deletedLabelCount)
WHERE {
  # parameters
  VALUES ( ?versionHistoryGraph ?type ?language ) {
    ( <http://zbw.eu/stw/version> "Descriptor" "de" )
  }
  GRAPH ?versionHistoryGraph {
    ?delta a sh:SchemeDelta ;
      sh:deltaTo/dc:identifier ?version ;
      sh:deltaFrom/sh:usingNamedGraph/sd:name ?oldVersionGraph ;
      dcterms:hasPart ?insertions ;
      dcterms:hasPart ?deletions .
    ?insertions a sh:SchemeDeltaInsertions ;
      sh:usingNamedGraph/sd:name ?insertionsGraph .
    ?deletions a sh:SchemeDeltaDeletions ;
      sh:usingNamedGraph/sd:name ?deletionsGraph .
  }
  # for each deleted label ...
  GRAPH ?deletionsGraph {
    [] skos:prefLabel | skos:altLabel ?label
  }
  # ... without being inserted otherwise (for another concept or property)
  FILTER NOT EXISTS {
    GRAPH ?insertionsGraph {
      [] skos:prefLabel | skos:altLabel ?label
    }
  }
  FILTER (lang(?label) = ?language)
  # filter on concept type
  GRAPH ?oldVersionGraph {
    ?concept skos:prefLabel | skos:altLabel ?label .
    # STW 8.04 used a different custom namespace URI
    BIND (uri(concat("http://zbw.eu/namespaces/skos-extensions/", ?type)) as ?oldConceptType)
    BIND (uri(concat("http://zbw.eu/namespaces/zbw-extensions/", ?type)) as ?conceptType)
    {
      ?concept a ?oldConceptType 
    } UNION {
      ?concept a ?conceptType
    }
  }
}
GROUP BY ?version
ORDER BY ?version
