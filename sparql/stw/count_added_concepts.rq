PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dsv: <http://purl.org/iso25964/DataSet/Versioning#>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>
PREFIX sh: <http://purl.org/skos-history/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX skosxl: <http://www.w3.org/2008/05/skos-xl#>
PREFIX xhv: <http://www.w3.org/1999/xhtml/vocab#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
# count concepts inserted per version
#
# (additional type skos:Concept introduced in 8.08)
#
SELECT ?version (str(count(distinct ?concept)) as ?addedConceptCount)
WHERE {
  # parameters
  VALUES ( ?versionHistoryGraph ?conceptType ) {
    ( <http://zbw.eu/stw/version> zbwext:Descriptor )
  }
  GRAPH ?versionHistoryGraph {
    ?delta a sh:SchemeDelta ;
      sh:deltaTo ?newVHR ;
      sh:deltaFrom ?oldVHR ;
      dcterms:hasPart ?insertions ;
      dcterms:hasPart ?deletions .
    ?newVHR xhv:prev ?oldVHR ;
      dc:identifier ?version .
    ?oldVHR sh:usingNamedGraph/sd:name ?oldVersionGraph .
    ?insertions a sh:SchemeDeltaInsertions ;
      sh:usingNamedGraph/sd:name ?insertionsGraph .
    ?deletions a sh:SchemeDeltaDeletions ;
      sh:usingNamedGraph/sd:name ?deletionsGraph .
  }
  # for each inserted concept, a (newly inserted) prefLabel must exist ...
  GRAPH ?insertionsGraph {
    ?concept skosxl:prefLabel|skos:prefLabel ?prefLabel
  }
  # ... which did not exist in the old version
  FILTER NOT EXISTS {
    GRAPH ?oldVersionGraph {
      ?concept ?p []
    }
  }
  # filter on concept type
  GRAPH ?versionGraph {
    ?concept a ?conceptType .
  }
}
GROUP BY ?version
ORDER BY ?version
