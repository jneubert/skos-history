PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dsv: <http://purl.org/iso25964/DataSet/Versioning#>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>
PREFIX sh: <http://purl.org/skos-history/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xhv: <http://www.w3.org/1999/xhtml/vocab#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
# Identify concepts inserted with a certain version,
# ordered by the second level subject category.
# Subject categories have a different concept type (subclass of
# skos:Concept) and form mono-hierarchies within STW. The top
# level subject categories are the sub-thesauri. A descriptor
# often belongs to more than one subject category.
#
SELECT distinct ?secondLevelCategory (str(max(?secondLevelLabel)) AS ?secondLevelCategoryLabel)
(str(count(distinct ?concept)) as ?totalConcepts)
WHERE {
  # parameters
  VALUES ( ?versionHistoryGraph ?conceptType ?oldVersion ?newVersion ?language ) {
    ( <http://zbw.eu/stw/version> zbwext:Descriptor undef undef "en" )
  }
  GRAPH ?versionHistoryGraph {
    # get the current version as default version
    ?versionset dsv:currentVersionRecord/dc:identifier ?latestVersion .
    # select the versions to actually use
    BIND(coalesce(?newVersion, ?latestVersion) AS ?versionSelected)
    # get the delta and via that the relevant graphs
    ?vhr a dsv:VersionHistoryRecord;
      dc:identifier $versionSelected ;
      sh:usingNamedGraph/sd:name ?versionGraph .
  }
  GRAPH ?versionGraph {
    ?scheme skos:hasTopConcept ?topConcept .
    ?topConcept skos:narrower ?secondLevelCategory .
    ?secondLevelCategory a zbwext:Thsys ;
      skos:prefLabel ?secondLevelLabel ;
      skos:narrower* ?category .
    ?category a zbwext:Thsys ;
      skos:narrower ?concept .
    ?concept a ?conceptType .
  }
  FILTER (lang(?secondLevelLabel) = ?language)
}
GROUP BY ?secondLevelCategory
ORDER BY ?secondLevelCategoryLabel
