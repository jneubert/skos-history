PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dsv: <http://purl.org/iso25964/DataSet/Versioning#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>
PREFIX sh: <http://purl.org/skos-history/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xhv: <http://www.w3.org/1999/xhtml/vocab#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
# show all labels deleted in the current version
#
SELECT DISTINCT ?concept (str(?prefLabel) AS ?conceptLabel) (str(?label) AS ?deletedLabel)
WHERE {
  GRAPH ?versionHistoryGraph {
    # parameters
    VALUES ( ?versionHistoryGraph ?conceptType ?oldVersion ?newVersion ?language ) {
      ( <http://zbw.eu/stw/version> zbwext:Descriptor undef undef "en" )
    }
    # get the current and the previous version as default versions
    ?versionset dsv:currentVersionRecord/xhv:prev/dc:identifier ?previousVersion .
    ?versionset dsv:currentVersionRecord/dc:identifier ?latestVersion .
    # select the versions to actually use
    BIND(coalesce(?oldVersion, ?previousVersion) AS ?oldVersionSelected)
    BIND(coalesce(?newVersion, ?latestVersion) AS ?newVersionSelected)
    # get the delta and via that the relevant graphs
    ?delta a sh:SchemeDelta ;
      sh:deltaFrom/dc:identifier ?oldVersionSelected ;
      sh:deltaTo/dc:identifier ?newVersionSelected ;
      sh:deltaFrom/sh:usingNamedGraph/sd:name ?oldVersionGraph ;
      sh:deltaTo/sh:usingNamedGraph/sd:name ?newVersionGraph ;
      dcterms:hasPart ?insertions ;
      dcterms:hasPart ?deletions .
    ?deletions a sh:SchemeDeltaDeletions ;
      sh:usingNamedGraph/sd:name ?deletionsGraph .
    ?insertions a sh:SchemeDeltaInsertions ;
      sh:usingNamedGraph/sd:name ?insertionsGraph .
  }
  # get all deleted labels
  GRAPH ?deletionsGraph {
    [] skos:prefLabel | skos:altLabel ?label
  }
  # ... which were not attached to some (other) concept now
  FILTER NOT EXISTS {
    GRAPH ?insertionsGraph {
      [] skos:prefLabel | skos:altLabel | zbwext:altLabelNarrower | zbwext:altLabelRelated ?label
    }
  }
  FILTER (lang(?label) = ?language)
  # restrict to a certain concept type and get additional information
  GRAPH ?oldVersionGraph {
    ?concept skos:prefLabel | skos:altLabel ?label .
    # ugly fix for a different custom namespace URI used in STW 8.04
    # REMARK: Previously, we used a `UNION`+`BIND` construct here (see git 
    # history). However, apparently using such a construct could introduce a 
    # significant performance hit.
    # SeeAlso: https://stackoverflow.com/questions/43373293/sparql-bind-inside-union-is-too-slow
    values (?ct) { 
      (zbwext:Descriptor)
      (<https://www.zbw.eu/de/namespaces/skos-extensions/Descriptor>)
    }
    ?concept a ?ct
  }
  GRAPH ?newVersionGraph {
    ?concept skos:prefLabel ?prefLabel .
  }
  # restrict output to a certain language
  FILTER (lang(?prefLabel) = ?language)
}
ORDER BY ?label
