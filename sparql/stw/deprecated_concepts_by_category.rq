PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dsv: <http://purl.org/iso25964/DataSet/Versioning#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>
PREFIX sh: <http://purl.org/skos-history/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xhv: <http://www.w3.org/1999/xhtml/vocab#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
# Identify descriptors deprecated with a certain version,
# ordered by the subject categories (first and second level)
# of the old version.
#
# Subject categories have a different concept type (subclass of
# skos:Concept) and form mono-hierarchies within STW. The top
# level subject categories are the sub-thesauri. A descriptor
# often belongs to more than one subject category.
#
SELECT DISTINCT ?topCategory (str(?topLabel) AS ?topCategoryLabel)
?secondLevelCategory (str(?secondLevelLabel) AS ?secondLevelCategoryLabel)
(?concept AS ?deprecatedConcept) (str(?prefLabel) AS ?deprecatedConceptLabel)
(?replacedBy as ?replacedByConcept) (str(?replacedByPrefLabel) AS ?replacedByConceptLabel)
WHERE {
  # parameters
  VALUES ( ?versionHistoryGraph ?conceptType ?oldVersion ?newVersion ?language ) {
    ( <http://zbw.eu/stw/version> zbwext:Descriptor undef undef "en" )
  }
  GRAPH ?versionHistoryGraph {
    # get the current and the previous version as default versions
    ?versionset dsv:currentVersionRecord/xhv:prev/dc:identifier ?previousVersion .
    ?versionset dsv:currentVersionRecord/dc:identifier ?latestVersion .
    # select the versions to actually use
    BIND(coalesce(?oldVersion, ?previousVersion) AS ?oldVersionSelected)
    BIND(coalesce(?newVersion, ?latestVersion) AS ?newVersionSelected)
    # get the delta and via that the relevant graphs
    ?delta a sh:SchemeDelta ;
      sh:deltaFrom/dc:identifier ?oldVersionSelected ;
      sh:deltaTo/dc:identifier ?newVersionSelected ;
      sh:deltaFrom/sh:usingNamedGraph/sd:name ?oldVersionGraph ;
      sh:deltaTo/sh:usingNamedGraph/sd:name ?newVersionGraph ;
      dcterms:hasPart ?insertions ;
      dcterms:hasPart ?deletions .
    ?deletions a sh:SchemeDeltaDeletions ;
      sh:usingNamedGraph/sd:name ?deletionsGraph .
    ?insertions a sh:SchemeDeltaInsertions ;
      sh:usingNamedGraph/sd:name ?insertionsGraph .
  }
  # identify the deprecated concepts
  GRAPH ?insertionsGraph {
    ?concept owl:deprecated true
  }
  # get the replacedBy information
  GRAPH ?newVersionGraph {
    # get concepts and restrict to conceptType
    # (no variant for type URI since newVersion 8.06)
    ?concept a ?conceptType .
    OPTIONAL {
      ?concept dcterms:isReplacedBy ?replacedBy .
      ?replacedBy skos:prefLabel ?replacedByPrefLabel .
    }
  }
  # get categories
  GRAPH ?oldVersionGraph {
    # get the top subject category (concept of subclass Thsys)
    # subject categories form a mono-hierarchy within STW
    ?concept skos:prefLabel ?prefLabel ;
      skos:broader ?category .
    ?category a zbwext:Thsys ;
      skos:broader* ?topCategory .
    ?topCategory skos:topConceptOf ?scheme ;
      skos:prefLabel ?topLabel ;
    #
    # get the second level category and it's label
    ?topCategory skos:narrower ?secondLevelCategory .
    ?category skos:broader* ?secondLevelCategory .
    ?secondLevelCategory skos:prefLabel ?secondLevelLabel .
  }
  FILTER (lang(?prefLabel) = ?language && lang(?secondLevelLabel) = ?language && lang(?topLabel) = ?language)
  FILTER (!bound(?replacedByPrefLabel) || lang(?replacedByPrefLabel) = ?language)
}
ORDER BY ?secondLevelCategoryLabel ?prefLabel

